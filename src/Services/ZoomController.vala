/*
 * SPDX-License-Identifier: GPL-3.0-or-later
 * SPDX-FileCopyrightText:  2017-2024 Lains
 *                          2025 Contributions from the ellie_Commons community (github.com/ellie-commons/)
 *                          2025-2026 Stella & Charlie (teamcons.carrd.co)
 */

/*************************************************/
/**
* Responsible to apply zoom appropriately to a window.
* Mainly, this abstracts zoom into an int and swap CSS classes
* As a treat it includes also the plumbing for ctrl+scroll zooming
*/
public class Jorts.ZoomController : Object {

    private static bool is_control_key_pressed = false;
    private weak Jorts.StickyNoteWindow window {get; set;}

    // Avoid setting this unless it is to restore a specific value, do_set_zoom does not check input
    private int _old_zoom;
    public int zoom {
        get {return _old_zoom;}
        set {do_set_zoom (value);}
    }

    public SimpleActionGroup actions { get; construct; }
    public const string ACTION_PREFIX = "zoom_controller.";
    public const string ACTION_ZOOM_OUT = "action_zoom_out";
    public const string ACTION_ZOOM_DEFAULT = "action_zoom_default";
    public const string ACTION_ZOOM_IN = "action_zoom_in";

    public static Gee.MultiMap<string, string> action_accelerators;

    private const GLib.ActionEntry[] ACTION_ENTRIES = {
        { ACTION_ZOOM_OUT, zoom_out},
        { ACTION_ZOOM_DEFAULT, zoom_default},
        { ACTION_ZOOM_IN, zoom_in}
    };


    public ZoomController (Jorts.StickyNoteWindow window) {
        this.window = window;
    }

    construct {
        actions = new SimpleActionGroup ();
        actions.add_action_entries (ACTION_ENTRIES, this);

        unowned var app = ((Gtk.Application) GLib.Application.get_default ());
        app.set_accels_for_action (ACTION_PREFIX + ACTION_ZOOM_OUT, {"<Control>minus", "<Control>KP_Subtract"});
        app.set_accels_for_action (ACTION_PREFIX + ACTION_ZOOM_DEFAULT, {"<Control>equal", "<Control>0", "<Control>KP_0"});
        app.set_accels_for_action (ACTION_PREFIX + ACTION_ZOOM_IN, {"<Control>plus", "<Control>KP_Add"});
    }

    /**
    * Handler. Wraps a zoom enum into the correct function-
    */
    public void zoom_changed (Jorts.ZoomType zoomtype) {
        debug ("Zoom changed!");
        switch (zoomtype) {
            case ZoomType.ZOOM_IN:              zoom_in (); return;          // vala-lint=double-spaces
            case ZoomType.DEFAULT_ZOOM:         zoom_default (); return;     // vala-lint=double-spaces
            case ZoomType.ZOOM_OUT:             zoom_out (); return;         // vala-lint=double-spaces
            default:                            return;                      // vala-lint=double-spaces
        }
    }

    /**
    * Wrapper to check an increase doesnt go above limit
    */
    public void zoom_in () {
        if ((_old_zoom + 20) <= ZOOM_MAX) {
            zoom = _old_zoom + 20;
        } else {
            Gdk.Display.get_default ().beep ();
        }
    }

    public void zoom_default () {
        if (_old_zoom != DEFAULT_ZOOM ) {
            zoom = DEFAULT_ZOOM;
        } else {
            Gdk.Display.get_default ().beep ();
        }
    }

    /**
    * Wrapper to check an increase doesnt go below limit
    */
    public void zoom_out () {
        if ((_old_zoom - 20) >= ZOOM_MIN) {
            zoom = _old_zoom - 20;
        } else {
            Gdk.Display.get_default ().beep ();
        }
    }

    /**
    * Switch zoom classes, then reflect in the UI and tell the application
    */
    private void do_set_zoom (int new_zoom) {
        debug ("Setting zoom: " + zoom.to_string ());

        // Switches the classes that control font size
        window.remove_css_class (Jorts.Zoom.from_int ( _old_zoom).to_css_class ());
        _old_zoom = new_zoom;
        window.add_css_class (Jorts.Zoom.from_int ( new_zoom).to_css_class ());
        window.textview.queue_refresh_indentation ();


        // Adapt headerbar size to avoid weird flickering
        window.view.headerbar.height_request = Jorts.Zoom.from_int (new_zoom).to_ui_size ();

        // Reflect the number in the popover
        window.popover.zoom = new_zoom;

        // Keep it for next new notes
        NoteData.latest_zoom = zoom;

        window.has_changed ();
    }

    public bool on_key_press_event (uint keyval, uint keycode, Gdk.ModifierType state) {
        if (keyval == Gdk.Key.Control_L || keyval == Gdk.Key.Control_R) {
            debug ("Press!");
            is_control_key_pressed = true;
        }

        return Gdk.EVENT_PROPAGATE;
    }

    public void on_key_release_event (uint keyval, uint keycode, Gdk.ModifierType state) {
        if (keyval == Gdk.Key.Control_L || keyval == Gdk.Key.Control_R) {
            debug ("Release!");
            is_control_key_pressed = false;
        }
    }

    public bool on_scroll (double dx, double dy) {
        debug ("Scroll + Ctrl!");

        if (!is_control_key_pressed) {
            return Gdk.EVENT_PROPAGATE;
        }

        zoom_changed (ZoomType.from_delta (dy));
        debug ("Go! Zoooommmmm");

        return Gdk.EVENT_STOP;
    }


    public void on_pinch (double dy) {
        debug ("Pinch!");

        // Delta is at 1 at rest
        zoom_changed (ZoomType.from_delta (dy - 1));
        debug ("Go! Zoooommmmm");

        //return Gdk.EVENT_STOP;
    }
}
